# Generating code in a macro

This example shows:
- How Haxe code can be generated by using either the
[`Context.parse()` API](http://api.haxe.org/haxe/macro/Context.html#parse) or the `macro` keyword for [expression reification](http://haxe.org/manual/macro-reification-expression.html).
- The difference between code that's executed at compile time (calculating the `build_time` timestamp in the macro function) and code that's executed at run time (calculating the `run_time` timestamp in expressions returned from the macro.) 
- How generated code is inlined in place of a macro function call.

```haxe
import haxe.macro.Context;
class Test
{
  public static function main() {
    // The expressions returned form the macros are injected here in main()
    trace_build_age_with_parse();
    trace_build_age_with_reification();
  }

  // This macro generates code using Context.parse()
  public static macro function trace_build_age_with_parse()
  {
    var build_time = Math.floor( Date.now().getTime()/1000 );

    var code = '{
      var run_time = Math.floor( Date.now().getTime()/1000 );
      var age = run_time - $build_time;
      trace("Right now it\'s "+run_time+", and this build is "+age+" seconds old");
    }';

    return Context.parse(code, Context.currentPos());
  }

  // This macro generates the same code using the macro keyword (expressions reification)
  public static macro function trace_build_age_with_reification()
  {
    var build_time = Math.floor( Date.now().getTime()/1000 );

    var e = macro {
      var run_time = Math.floor( Date.now().getTime()/1000 );
      var age = run_time - $v{ build_time };
      trace("Right now it's "+run_time+", and this build is "+age+" seconds old");
    };

    return e;
  }

}
```

## Usage

Compile this example to Neko with:

```
haxe -main Test -neko test.n
```

Run the program with `neko test.n` and observe the output:

```
Test.hx:6: Right now it's 1466034708, and this build is 3 seconds old
Test.hx:32: Right now it's 1466034708, and this build is 3 seconds old
```

Wait a few seconds and run it again:

```
Test.hx:6: Right now it's 1466034711, and this build is 6 seconds old
Test.hx:32: Right now it's 1466034711, and this build is 6 seconds old
```

Also, you can compile it to javascript and observe how the expressions the macro returned
are indeed inlined directly in the main() function. The `build_time` timestamp is now
simply a literal:

```
Test.main = function() {
	var run_time = Math.floor(new Date().getTime() / 1000);
	var age = run_time - 1466034832;
	console.log("Right now it's " + run_time + ", and this build is " + age + " seconds old");
	var run_time1 = Math.floor(new Date().getTime() / 1000);
	var age1 = run_time1 - 1466034832;
	console.log("Right now it's " + run_time1 + ", and this build is " + age1 + " seconds old");
};
```

> Author: [Jeff Ward](http://github.com/jcward) / [@jeff__ward](http://twitter.com/jeff__ward)
