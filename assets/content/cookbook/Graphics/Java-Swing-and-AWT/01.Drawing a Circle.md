#Java, Swing and AWT: Drawing a Circle
Swing and AWT for Java target are easy to use out of the box in Haxe, an ideal approach for creating a quick desktop application.

Swing is newer than AWT and is ideal for component functionality, and AWT is useful for more basic functionality. 
Our Main haxe class can extend a Frame ( AWT ) or a JFrame ( Swing ) both will work for creating applications.
The main reason to extend a JFrame is to allow us easily setup Mouse and Keyboard interaction.

( For web it's similar Main would extend an Applet ( AWT ) or a JApplet ( Swing ), but no one uses them anymore. )

#Main - JSwing window setup.
We start our swing application by extending Main and setting up an application window.
```haxe
package;
import java.lang.System;
import java.javax.swing.JPanel;
import java.javax.swing.JFrame;
import java.awt.Container;
import Sprite;// our custom JPanel
import java.awt.Graphics2D;
import java.awt.Color;
class MainCircle extends JFrame {
    
    public static function main(){ 
        new MainCircle(); 
    } 
    
    public function new(){
        // The view windows title.
        super('Graphics 2D Example');
        // Specify to use Hardware rendering.
        System.setProperty("sun.java2d.opengl","True");
        // Set window size to 700 pixels wide by 500 pixels high.
        setSize( 700, 500 );
        // Add a close button to window.
        setDefaultCloseOperation( JFrame.EXIT_ON_CLOSE );
        // Set the background to black.
        setBackground( Color.black );
        // Centre the view window in centre of users screen.
        setLocationRelativeTo(null);
        // code to implement later ..
        createCircle(); 
        // Show the view window.
        setVisible( true );
    }
    function createCircle(){
        // to implement later.
    }
}
```
We need to create a folder for the output files, mainCircle and we need 'compile.hxml'. 
First we compile the Haxe Java, then "--next" create the Java application with Java.
###compile.hxml
```
-java mainCircle
-main MainCircle
# after Haxe has compiled into Java lets run the code
--next
# move to the correct folder
-cmd cd mainCircle
# run our application
-cmd java -jar MainCircle.jar
```
If we run the compile.hxml from our terminal/commandline 
```
haxe compile.hxml
```

You should get a window with a title and a black background. 
If not then check that you have Java and Haxe setup properly by referring to other tutorials.

##JPanel - Drawing a Circle
Swing is a component structure and provides double buffering out of the box, to draw a circle we can extend a JPanel, and 
override the native paintComponent to access the components built in drawing or painting function.
Rather than commit our JPanel to a specific drawing function instead I prefer to pass a drawing method to allow more flexible use.
Sprite is a generic name traditionally used for a basic interactive element within a game and not just a Flash specific term.
```haxe
package;
import java.javax.swing.JPanel;
import java.awt.Graphics;
import java.awt.Graphics2D;
import java.awt.RenderingHints;

class Sprite extends JPanel {
    // drawing function set externally to the JPanel
    public var paintFunction:   Graphics2D -> Void;
    public function new( paintFunction_: Graphics2D -> Void ){ 
      super( true );
      paintFunction = paintFunction_;
    }
    
    @:overload
    override public function paintComponent( g: Graphics ){
        // magic used to stop flickering when redrawing for animation later.        
        super.paintComponent( g );
        // Strange but we need to cast so we can paint        
        var g2D: Graphics2D = cast g;
        // make drawing antialias so smooth not pixelated edges        
        g2D.setRenderingHint( RenderingHints.KEY_ANTIALIASING
                            , RenderingHints.VALUE_ANTIALIAS_ON );
        g2D.setRenderingHint( RenderingHints.KEY_RENDERING
                            , RenderingHints.VALUE_RENDER_QUALITY );
        paintFunction( g2D );
        // release memory of Graphics2D after finished drawing with it.        
        g2D.dispose();
    }
  
}
```
To use this code we must add the JPanel to our JFrame and pass a drawing function to it.
Our main class already has an empty method where we can add this code.
```haxe
    function createCircle(){
        var drawCircle = function( g2D: Graphics2D ){
                    // set drawing color
                    g2D.setColor(Color.red);
                    // draw a fill oval, x, y, wide, hi
                    g2D.fillOval( 300, 120, 150, 150 );
        }
        var circle = new Sprite( drawCircle );
        var container = getContentPane();
        container.add( circle );
    }
```
The full code can be downloaded here:
### Git link
<https://github.com/nanjizal/CookingHaxe/tree/master/Graphics/Java-Swing-and-AWT/Drawing-a-Circle>
