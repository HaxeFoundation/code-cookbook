#SVG - Drawing a Circle

Dynamically creating SVG within the browser with Javascript can be painful. 
With Haxe Javascript we can use abstracts to hide the uglier parts of working with SVG.
In this tutorial we will use abstracts to help make manipulation of SVG simpler. 
It is assumed you have explored abstracts in Haxe in depth before trying this tutorial, and have a rough understanding of SVG.

##Summary First
So with Haxe's abstracts it's possible to create SVG with really clean code, and the structural overheads largely disappear in the compiled javascript. 

```haxe
package;
import svgShapes.SvgRoot;
import svgShapes.SvgCircle;

class MainCircle{
    
    public static function main(){
        new MainCircle();
    }
    var svgRoot: SvgRoot;
    
    public function new(){
        svgRoot = new SvgRoot();
        svgRoot.width = 800;
        svgRoot.height = 800;
        createCircle();
    }
    
    public function createCircle(){
        var svgCircle = new SvgCircle();
        svgCircle.cx = 200;
        svgCircle.cy = 200;
        svgCircle.r = 50;
        svgCircle.setFillAndAlpha( 0xff0000, 1 );
        svgRoot.add( svgCircle );
    }
}
```
Lets look at how SVG is created.

##SVG nodes - SVGElement, overview.
To create the nodes of SVG we create SVGElements using a string type and then set attributes. 
So a svg circle node would be created:
```haxe
var nameSpace = "http://www.w3.org/2000/svg"
var svgElement: SVGElement = cast Browser.document.createElementNS( nameSpace, 'circle' );
```
and then it's attributes can be set such as the radius.
```haxe
svgElement.setAttribute( 'r','50' );
```
and we can also get it's attributes.
```haxe
var radius = svgELement.getAttribute( 'r' );
```

##Setting Up SVG

To create SVG we create a root node that we append to the body ( or to an element ) of our html document.
```haxe 
// create SVGElement
var svgElement: SVGElement = cast Browser.document.createElementNS( "http://www.w3.org/2000/svg", 'svg' );
// cast as an element to allow us to append it the root node to the document body
var element: Element = cast svgElement;
Browser.document.body.appendChild( element );
```
With SVG or canvas we need to setup the style so that coordinates are relative to the top corner.
This is done using the 'style' property of the svg node.
```haxe
var style: CSSStyleDeclaration = element.style;
style.paddingLeft = "0px";
style.paddingTop = "0px";
style.left = Std.string( 0 + 'px' );
style.top = Std.string( 0 + 'px' );
style.position = "absolute";
```
The default svg node's visible area is not very big so it's important to set it's width and height attributes.
So the width of the svg root node can be set:
```haxe
svgElement.setAttribute('width','800');
```
But really this is ugly, it might be ok for javascript developers setting attributes like this but it's pretty inflexible if we want to tween or animate attributes.
So instead we can wrap the 'set' and 'get' attribute methods for width and height, so for width we can create a getter/setter.
```haxe
    public var width( get, set ): Int;
    inline public function set_width( width_: Int ):Int {
        // pass in a Integer width value and convert it to a String for the setAttribute method.
        this.setAttribute( "width", Std.string( width_ ) );
        return( width_ );
    }
    inline public function get_width(): Int {
        // convert String value back an Integer for easier manipulation.
        return Std.parseInt( this.getAttribute( "width" ) );
    }
```
if we wrap the SVGElement in an abstract we can then call these properties directly on the abstract.
```haxe
svgRootAbstract.width = 800;
```
which allows much easier manipulation of the svg node's width. 
##So the full abstract for our root svg element:
```haxe
package svgShapes;
import js.Browser;
import js.html.svg.SVGElement;
import js.html.Element;
import js.html.CSSStyleDeclaration;
import js.html.Node;

abstract SvgRoot( SVGElement ) from SVGElement to SVGElement {
    inline public static var svgNameSpace: String = "http://www.w3.org/2000/svg" ;
    inline public function new( ?e: SVGElement ){
        if( e == null ){
            this = create();
        } else {
            this = e;
        }
    }
    
    inline static public function create(): SvgRoot {
        var svgElement: SVGElement = cast Browser.document.createElementNS( svgNameSpace, 'svg' );
        var element: Element = cast svgElement;
        var style: CSSStyleDeclaration = element.style;
        style.paddingLeft = "0px";
        style.paddingTop = "0px";
        style.left = Std.string( 0 + 'px' );
        style.top = Std.string( 0 + 'px' );
        style.position = "absolute";
        Browser.document.body.appendChild( element );
        var svgRoot: SvgRoot = svgElement;
        return svgRoot;
    }
    // allows us to add children svg elements 
    inline public function add( element: SVGElement ):Void {
        var node: Node = cast element;
        this.appendChild( node );
    }
    
    public var width( get, set ): Int;
    inline public function set_width( width_: Int ):Int {
        this.setAttribute( "width", Std.string( width_ ) );
        return( width_ );
    }
    inline public function get_width(): Int {
        return Std.parseInt( this.getAttribute( "width" ) );
    }
    public var height( get, set ): Int;
    inline public function set_height( height_: Int ):Int {
        this.setAttribute( "height", Std.string( height_ ) );
        return( height_ );
    }
    inline public function get_height(): Int {
        return Std.parseInt( this.getAttribute( "height" ) );
    }
}
```
This abstract allows us to create the root svg node very cleanly hiding the ugly parts of javascript SVG.
##MainCircle - main class.
So to use this we can create a main class and setup very simply to initialize this abstract, notice the createCircle is for the moment removed.
```haxe
package;
import svgShapes.SvgRoot;
// import svgShapes.SvgCircle; // not yet implemented

class MainCircle{
    
    public static function main(){
        new MainCircle();
    }
    var svgRoot: SvgRoot;
    
    public function new(){
        svgRoot = new SvgRoot();
        svgRoot.width = 800;
        svgRoot.height = 800;
        createCircle();
    }
    
    public function createCircle(){
        // not yet implemented
    }
}
```
##SvgCircle - Abstracting a SVG circle.

We have already seen how to create a SVGElement circle. 
So we can follow exactly the same approach and wrap all the set and get attributes in an abstract to allow us easy control of properties.

```haxe
package svgShapes;

import js.Browser;
import js.html.svg.SVGElement;

abstract SvgCircle( SVGElement ) from SVGElement to SVGElement {
    inline public static var svgNameSpace: String = "http://www.w3.org/2000/svg" ;
    public inline function new( ?e: SVGElement ){
        if( e == null ){
            this = create();
        } else {
            this = e;
        }
    }
    inline static public function create(): SvgCircle {
        var svgCircle: SVGElement = cast Browser.document.createElementNS( svgNameSpace, 'circle' );
        return svgCircle;
    }
    public var cx( get, set ): Float;
    inline public function set_cx( cx_: Float ):Float {
        this.setAttribute( "cx", Std.string( cx_ ) );
        return( cx_ );
    }
    inline public function get_cx(): Float {
        return Std.parseFloat( this.getAttribute( "cx" ) );
    }
    public var cy( get, set ): Float;
    inline public function set_cy( cy_: Float ):Float {
        this.setAttribute( "cy", Std.string( cy_ ) );
        return( cy_ );
    }
    inline public function get_cy(): Float {
        return Std.parseFloat( this.getAttribute( "cy" ) );
    }
    public var r( get, set ): Float;
    inline public function set_r( r_: Float ):Float {
        this.setAttribute( "r", Std.string( r_ ) );
        return( r_ );
    }
    inline public function get_r(): Float {
        return Std.parseFloat( this.getAttribute( "r" ) );
    }
    public var strokeWidth( get, set ): Float;
    inline public function set_strokeWidth( strokeWidth_: Float ):Float {
        this.setAttribute( "stroke-width", Std.string( strokeWidth_ ) );
        return( strokeWidth_ );
    }
    inline public function get_strokeWidth(): Float {
        return Std.parseFloat( this.getAttribute( "stroke-width" ) );
    }
    
    public var fill( get, set ): String;
    inline public function set_fill( fill_: String ): String {
        this.setAttribute( "fill", fill_ );
        return fill_;
    }
    inline public function get_fill():String {
        return this.getAttribute( "fill" );
    }
    
    inline public function setFillAndAlpha( col: Int, ?alpha: Float ): Void {
        fill = getColor( col, alpha );
    }
    public var strokeFill( get, set ): String;
    inline public function set_strokeFill( fill_: String ): String {
        this.setAttribute( "stroke", fill_ );
        return fill_;
    }
    inline public function get_strokeFill():String {
        return this.getAttribute( "stroke" );
    }
    inline public function setStrokeFillAndAlpha( col: Int, ?alpha: Float ): Void {
        strokeFill = getColor( col, alpha );
    }
    public static inline function getColor( col: Int, ?alpha: Float ):String{
		if( alpha != null && alpha != 1.0 ){
			var r = (col >> 16) & 0xFF;
			var g = (col >> 8) & 0xFF;
			var b = (col) & 0xFF;
			 return 'rgba($r,$g,$b,$alpha)';
		} else {
			return '#' + StringTools.hex( col, 6 );
		}
	}
}
```
The getColor function is a bit complex but it's just to help us set fill and stroke colors of the circle both as solid colors or alpha.
##Creating a Circle using our SvgCircle abstract.

In our main class we have a 'createCircle' method we can create a red SVG circle here.
```haxe
    public function createCircle(){
        var svgCircle = new SvgCircle();
        svgCircle.cx = 200;
        svgCircle.cy = 200;
        svgCircle.r = 50;
        svgCircle.setFillAndAlpha( 0xff0000, 1 );    
        svgRoot.add( svgCircle );
    }
```
So the extra effort of the abstract hides all the ugly parts of creating a circle, but it also makes it much easier for you to extend this tutorial to animate and create lots of circles.
Obviously you can explore the different SVGElements and perhaps wrap them in abstracts for easy use.
If you look at the source you will see that the all the abstract code disappears at compile, so the approach really has very little overhead and allows us to build and manipulate complex SVG structures in code fairly easily.
##git source
<https://github.com/nanjizal/CookingHaxe/tree/master/Graphics/Create-SVG/01.Drawing-a-Circle>
##view result
https://cdn.rawgit.com/nanjizal/CookingHaxe/master/Graphics/Create-SVG/01.Drawing-a-Circle/index.html
